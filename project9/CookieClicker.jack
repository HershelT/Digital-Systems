class CookieClicker {
    field int cookies;
    // field int frameCounter;
    field int cookieWidth;
    field int screenMid;
    field int cookieRow; // Row where the cookie will be drawn
    field boolean isDrawingFullCookie; //Flag to track if cookie is pressed

    //Displays cookie count at once
    field boolean displayVisualOfCookies; //Flag to track if cookie is pressed
    field boolean displayVisualOfUpgrades; //Flag to track if cookie is pressed

    //Prev ent spamming with checking if any key is pressed
    field boolean isAnyKeyPressed; // Flag to track if any key is pressed

    // count of upgrades
    field int fullCookieTimer; //Timer to track how long the cookie is shown for after pressed
    
    field int cursors; // Number of cursors
    field int cursorTimer; // Timer for cursors
    field Upgrades cursorUpgrade; // Cursor upgrade object

    field int grandmas; // Number of grandmas
    field int grandmaTimer; // Timer for grandmas
    field Upgrades grandmaUpgrade; // Grandma upgrade object


    constructor CookieClicker new() {
        let cookies = 10000;
        // let frameCounter = 0;
        let cookieWidth = 32;
        let screenMid = 128 - (cookieWidth / 2); //middle of the screen
        let cookieRow = 32; // Row where the cookie will be drawn

        let displayVisualOfCookies = false; // Initialize to false

        let isAnyKeyPressed = false; // Initialize to false

        let isDrawingFullCookie = false;
        let displayVisualOfUpgrades = false;

        let fullCookieTimer = 0;

        // Initialize upgrades
        let cursors = 0;
        let cursorTimer = 0;
        // Create new Upgrades object of cursor for (cost, worth, multiplier)
        let cursorUpgrade = Upgrades.new(15, 1, 115); // Create new Upgrades object
        
        let grandmas = 0;
        let grandmaTimer = 0;
        // Create new Upgrades object of grandma for (cost, worth, multiplier)
        let grandmaUpgrade = Upgrades.new(100, 10, 115); // Create new Upgrades object

        return this;
    }

    // displayUpgrades method
    method void displayUpgrades() {
        do Output.moveCursor(0, 0);
        do Output.printInt(cursors);
        do Output.printString(" - Cursors!");
        // Output price of cursor
        do Output.moveCursor(1, 0);
        do Output.printInt(cursorUpgrade.getCost());
        do Output.printString(" - Cost");

        // Output the number of grandmas
        do Output.moveCursor(2, 0);
        do Output.printInt(grandmas);
        do Output.printString(" - Grandmas!");
        // Output price of grandma
        do Output.moveCursor(3, 0);
        do Output.printInt(grandmaUpgrade.getCost());
        do Output.printString(" - Cost");
        return;
    }

    // method void displayCookies() {
    //     do Output.moveCursor(0, 28);
    //     do Output.printString("Cookies: ");
    //     do Output.printInt(cookies);
    //     do Output.printString("            ");
    //     do Sys.wait(10);
    //     return;
    // }

    // HandleCursorLogic()
    method void handleUpgradeLogic() {
        if (cursors > 0) {
            let cursorTimer = cursorTimer + 1;
            if (cursorTimer > 20) { // Every 20 frames (1 second)
                let cookies = cookies + (cursors * cursorUpgrade.worth());
                let cursorTimer = 0;
                // update flag to display the number of cookies
                let displayVisualOfCookies = true;
            }
        }

        // Handle grandma logic
        if (grandmas > 0) {
            let grandmaTimer = grandmaTimer + 1;
            if (grandmaTimer > 100) { // Every 100 frames (5 seconds)
                let cookies = cookies + (grandmas * grandmaUpgrade.worth());
                let grandmaTimer = 0;
                // update flag to display the number of cookies
                let displayVisualOfCookies = true;
            }
        }

        return;
    }



    method void run() {
        var char key;
        var int frameCounter;
        
        // Cost of upgrades
        var int cursorCost;
        var int grandmaCost;

        //Draw the starting cookie that is not filled in
        do CookieDrawings.drawPressedCookie(screenMid + (cookieRow * 32));
        do Output.moveCursor(1, 24);
        do Output.printString("Spacebar for cookies!");
        do ScoreBoard.printCookies(cookies);
        do displayUpgrades(); // Display the number of cursors

        while (true) {
            let key = Keyboard.keyPressed();

            //Handles updating of visuals 
            if (displayVisualOfCookies) {
                // do displayCookies();
                do ScoreBoard.printCookies(cookies);
                let displayVisualOfCookies = false;
            }

            if (displayVisualOfUpgrades) {
                // do displayUpgrades();
                do Output.moveCursor(0, 0);
                do Output.printString("Cursors: ");
                do Output.printInt(cursors);
                do Output.printString("        ");
                do Sys.wait(5);
                // Output price of cursor
                do Output.moveCursor(1, 0);
                do Output.printString("Cost: ");
                do Output.printInt(cursorUpgrade.getCost());
                do Output.printString("        ");
                do Sys.wait(5);
                // Output the number of grandmas
                do Output.moveCursor(2, 0);
                do Output.printString("Grandmas: ");
                do Output.printInt(grandmas);
                do Output.printString("        ");
                do Sys.wait(5);
                // Output price of grandma
                do Output.moveCursor(3, 0);
                do Output.printString("Cost: ");
                do Output.printInt(grandmaUpgrade.getCost());
                do Output.printString("        ");
                do Sys.wait(10);
                let displayVisualOfUpgrades = false;
            }


            // Handle key presses
            if (key=0) { // If no key is pressed
                let isAnyKeyPressed = false; // Set flag to false
            } else {
                if (~isAnyKeyPressed) { // If no key was pressed before
                    let isAnyKeyPressed = true; // Set flag to true
                
                    // Check for *new* press of spacebar
                    if (key = 32) { // Spacebar
                        let cookies = cookies + 1;
                        let isDrawingFullCookie = true; // Set flag to draw full cookie
                        let fullCookieTimer = 0; // Reset timer
                        let displayVisualOfCookies = true; // Set flag to display cookies
                        do Sys.wait(5); // Prevents spamming of keys
                    }

                    // Handle upgrades key presses

                    // Cursor upgrade
                    if (key = 99) { // c key
                        let cursorCost = cursorUpgrade.getCost();
                        if (cookies > (cursorCost - 1)) {
                            let cookies = cookies - cursorCost; // Deduct cost
                            let cursors = cursors + 1; // Add cursor
                            do cursorUpgrade.upgrade(); // Upgrade cursor cost
                            let displayVisualOfUpgrades = true; // Set flag to display upgrades
                            do Sys.wait(5);
                        }
                    }

                    // Grandma upgrade
                    if (key = 103) { // g key
                        let grandmaCost = grandmaUpgrade.getCost();
                        if (cookies > (grandmaCost - 1)) {
                            let cookies = cookies - grandmaCost; // Deduct cost
                            let grandmas = grandmas + 1; // Add grandma
                            do grandmaUpgrade.upgrade(); // Upgrade grandma cost
                            let displayVisualOfUpgrades = true; // Set flag to display upgrades
                            do Sys.wait(5); // Prevents spamming of keys
                        }
                    }
                }
            }
            // Handle logic for upgrades
            do handleUpgradeLogic(); // Handle cursor logic


            let frameCounter = frameCounter + 1;
            if (frameCounter > 10) {
                //Reset frame counter
                let frameCounter = 0;
            }
            // Check if cookie is being pressed and draw the full cookie
            else {
                if (isDrawingFullCookie) {
                    do CookieDrawings.drawCookie(screenMid + (cookieRow * 32));
                    let fullCookieTimer = fullCookieTimer + 1;
                    if (fullCookieTimer > 4) { // Show cookie for 20 frames (1 second)
                        let isDrawingFullCookie = false; // Stop drawing full cookie
                        do CookieDrawings.drawPressedCookie(screenMid + (cookieRow * 32));
                        let fullCookieTimer = 0; // Reset timer
                        do Sys.wait(5); // Prevents spamming of keys
                    }
                }
            }

            if (key = 140) { // Escape
                return;
            }
            
            do Sys.wait(5);
        }
        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}